plugins {
	id 'application'
	id 'io.spring.dependency-management' version '1.0.4.RELEASE'
	id 'org.springframework.boot' version '2.0.1.RELEASE'
	id 'org.owasp.dependencycheck' version '3.1.2'
	id 'com.github.ben-manes.versions' version '0.17.0'
	id 'org.sonarqube' version '2.6.2'
	id 'jacoco'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.github.ben-manes.versions'

group = 'uk.gov.hmcts.reform.rpa'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
mainClassName = 'uk.gov.hmcts.reform.rpa.incourtpres.IncourtPresApplication'

dependencyCheck {
	// Specifies if the build should be failed if a CVSS score above a specified level is identified.
	// range of 0-10 fails the build, anything greater and it doesn't fail the build
	failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'false' ? 11 : 0
	suppressionFile = 'dependency-check-suppressions.xml'
	analyzers {
		// Disable scanning of .NET related binaries
		assemblyEnabled = false
	}
}
repositories {
	maven {
		url "https://dl.bintray.com/hmcts/hmcts-maven"
	}
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }

	jcenter()
	mavenLocal()
}

def versions = [
		postgresql         : '42.2.2',
		springBoot         : '2.0.1.RELEASE',
		hmctsJavaLogging   : '3.0.0'
]

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
//	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-websocket')
	compile("org.postgresql:postgresql:${versions.postgresql}")
	compile("uk.gov.hmcts.reform:java-logging-spring:${versions.hmctsJavaLogging}")
	compile("uk.gov.hmcts.reform:java-logging-httpcomponents:${versions.hmctsJavaLogging}")
	compile("uk.gov.hmcts.reform:java-logging-appinsights:${versions.hmctsJavaLogging}")
	runtime('org.springframework.boot:spring-boot-devtools')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('com.h2database:h2:1.4.197')
}

sourceSets {
	aat {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
			srcDir('src/aat/java')
		}
		resources {
			srcDir('src/aat/resources')
		}
	}
}

task functional(type: Test) {
	group = 'Delivery pipeline'
	description = 'Executes functional tests against an AAT In Court Presentation instance'
	setTestClassesDirs(sourceSets.aat.output.classesDirs)
	setClasspath(sourceSets.aat.runtimeClasspath)
	include "uk/gov/hmcts/reform/incourtpres/functional/**"

	environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task smoke(type: Test) {
	group = 'Delivery pipeline'
	description = 'Executes non-destructive smoke tests against a running In Court Presentation instance'
	setTestClassesDirs(sourceSets.aat.output.classesDirs)
	setClasspath(sourceSets.aat.runtimeClasspath)
	include "uk/gov/hmcts/reform/incourtpres/smoke/**"

	environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

bootJar {
	baseName 'rpa-icp'
	archiveName = "$baseName"+".jar"
	destinationDir = file("$rootDir/build/libs")
	manifest {
		attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
	}
}

